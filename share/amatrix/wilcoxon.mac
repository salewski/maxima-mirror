/* copyright 2007 by Robert Dodier
 * I release this file under the terms of the GNU General Public License.
 */

wilcoxon (X, C) := block ([X1, X0, X10, R, n1, n0, Rsum, W],

    X1 : X [C = 1, 1],
    X0 : X [C = 0, 1],
    n1 : nrows (X1),
    n0 : nrows (X0),
    if n1 = 0 or n0 = 0
        then 1/2
        else
           (X10 : append (elements (X1), elements (X0)),
            R : ranks (X10),
            Rsum : sum (R[i], i, 1, n1),
            W : (Rsum - n1 * (n1 + 1) / 2) / (n1 * n0)));

load ("combinatorics");

ordering (L, [optional]) :=
    block ([P, P1: if optional = [] then 'orderlessp else optional[1]],
           P: buildq ([P1], lambda ([a, b], P1 (first (a), first (b)))),
           map (second, sort (makelist ([L[i], i], i, 1, length (L)), P)));

ranks (L, [optional]) :=
    block ([P: if optional = [] then 'orderlessp else optional[1],
            ties_method: if length (optional) < 2 then 'mean_rank else optional[2]],
           ranks1 (L, P, ties_method));

ranks1 (L, P, ties_method) :=
    block ([order: ordering (L, P),
            eq: buildq ([P], lambda ([a, b], not P(a, b) and not P(b, a)))],
           %ranks: perm_inverse (order),
           if ties_method # 'distinct_ranks
               then block ([sorted: permute (order, L), runs],
                           runs: find_runs_nontrivial (sorted, eq),
                           for e in runs do block ([tied_rank],
                               if ties_method = 'mean_rank
                                   then tied_rank : (1/second(e)) * sum (%ranks [order[i]], i, first(e), first(e) + second(e) - 1)
                               elseif ties_method = 'min_rank
                                   then tied_rank : %ranks[ order[ first(e) ] ]
                               elseif ties_method = 'max_rank
                                   then tied_rank : %ranks[ order[ first(e) + second(e) - 1 ] ]
                               else error ("ranks: ties_method must be distinct_ranks, mean_rank, min_rank, or max_rank; found:", ties_method),
                               for i : first(e) thru first(e) + second(e) - 1 do %ranks [order[i]] : tied_rank)),
               /* else: return %ranks without modification */
           %ranks);

load ("descriptive");

find_runs_nontrivial (x, eq) :=
    if x = [] then []
        else block ([runs: find_runs (x, eq), by_twos],
                    /* rearrange runs(...) to list of pairs as expected in this function */
                    map (lambda ([a, b], [a, b]), cons (1, 1 + accumulate ("+", rest (runs@lengths, -1))), runs@lengths),
                    sublist (%%, lambda ([e], second (e) > 1)));
